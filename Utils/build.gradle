/*
 * This build file was generated by the Gradle 'init' task.
 *
 * This generated file contains a sample Java Library project to get you started.
 * For more details take a look at the Java Libraries chapter in the Gradle
 * user guide available at https://docs.gradle.org/4.1/userguide/java_library_plugin.html
 */

// Apply the java-library plugin to add support for Java Library
plugins {
    id "java-library"
    id "maven-publish"
    id "eclipse"
    id "idea"
    id "signing"
    //id "org.owasp.dependencycheck" version "6.1.0"
}
Properties props = new Properties()
def propFile = file('gradle.properties')
def artifactory_Url="https://artifactory.distri-mind.fr/artifactory"
def user=""
def pwd=""
if (propFile.canRead())
{
    props.load(new FileInputStream(propFile))

   
	if (props!=null && props.containsKey('artifactory_contextUrl'))
		artifactory_Url="${artifactory_contextUrl}"
	if (props!=null && props.containsKey('artifactory_user'))
		user="${artifactory_user}"	
	if (props!=null && props.containsKey('artifactory_password'))
		pwd="${artifactory_password}"	
}

	


eclipse {
    classpath {
       downloadSources=true
    }
}

// In this section you declare where to find the dependencies of your project

repositories {


   	maven {
       		url "${artifactory_Url}/gradle-release"
       		if (user!="")
       		{
	       		credentials {
	              username "${user}"
	              password "${pwd}"
	            }
	         }
   	}
}

dependencies {
    testImplementation 'org.testng:testng:7.1.0'
    testImplementation 'org.hamcrest:hamcrest-core:2.2'

    api(group:'com.distrimind.bcfips', name: 'BouncyCastle-FIPS', version: '1.0.2-DMRECOMPILE')
    api(group:'com.distrimind.bouncycastle', name: 'core', version: '1.67-DMRECOMPILE')
    api(group:'com.distrimind.bouncycastle', name: 'prov', version: '1.67-DMRECOMPILE')


    testImplementation(group:'com.distrimind.gnu', name: 'Gnu-Crypt', version: '1.3.0')
    //api(group:'commons-codec', name: 'commons-codec', version: '1.14')
    api(group:'org.yaml', name: 'snakeyaml', version: '1.27')
    testImplementation(group:'org.apache.commons', name: 'commons-math3', version: '3.6.1')

}

def javaVersion="8"
sourceCompatibility = javaVersion
targetCompatibility= javaVersion

idea {
    project {
        jdkName = javaVersion
        languageLevel = javaVersion
    }
}

version='??.??.??ToDefine'


task generateVersionsFile(type:JavaExec, dependsOn:classes) {
    if (project.hasProperty('assemble') || project.hasProperty('build') || project.hasProperty('jar')) {
        def versionBuildFile = file('src/main/resources/com/distrimind/util/build.txt')

        /*Wrapping inside a method avoids auto incrementing on every gradle task run. Now it runs only when we build apk*/
        if (versionBuildFile.canRead()) {
            def line = 0
            versionBuildFile.withReader { line = it.readLine() }
            def buildNumber = line.toInteger() + 1
            versionBuildFile.withWriter { out ->
                out.println buildNumber
            }
        } else {
            versionBuildFile.withWriter { out ->
                out.println 1
            }
        }

        main = 'com.distrimind.util.Utils'
        classpath = sourceSets.main.runtimeClasspath
    }

}
task generateMDFiles (dependsOn: generateVersionsFile) {

    if (project.hasProperty('assemble') || project.hasProperty('build') || project.hasProperty('jar')) {


        doLast {
            def lastVersionFile = new File('../lastVersion.md')


            project.version = lastVersionFile.getText('UTF-8').trim()

            def mdFile = new File('../README_TO_EDIT.md')
            if (mdFile.canRead()) {
                def mdFileDest = new File('../README.md')

                mdFileDest.newWriter().withWriter { w ->
                    w << mdFile.getText('UTF-8').replaceAll("//PROJECT_VERSION//", version.toString())

                }
            }
            ext.artifactVersion = String.valueOf(project.version)
            def pomFile = new File('build/publications/mavenJava/pom-default.xml')
            if (pomFile.canRead()) {
                String t = pomFile.getText('UTF-8').replaceAll("\\?\\?.\\?\\?.\\?\\?ToDefine", version.toString())

                pomFile.newWriter().withWriter { w ->
                    w << t
                }
            }


        }
    }


}
allprojects {
	group='com.distrimind.util'

    if (JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_1_9)) {
        compileJava.options.compilerArgs.addAll(['--release', javaVersion] as Collection<? extends String>)
        javadoc.options.addStringOption("html5", "-quiet")
    }

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked"
        }
    }
}





task sourcesJar(type: Jar, dependsOn: classes) {

    archiveClassifier.set("sources")
    from sourceSets.main.allSource
    from "../LICENSE"
    from "../versions.md"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set("javadoc")
    from javadoc.destinationDir
    from "../LICENSE"
    from "../versions.md"
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}



jar{
    dependsOn generateMDFiles
    manifest {
        attributes('Implementation-Title': project.name,
                   'Implementation-Version': project.version)
    }
    from "../LICENSE"
    from "../versions.md"

}

test {
     
     // enable TestNG support (default is JUnit)
   	useTestNG
   	{
   		suites 'src/test/java/com/distrimind/util/AllTestsNG.xml'
   	}
  
   // set heap size for the test JVM(s)
   minHeapSize = "1m"
   maxHeapSize = "6g"
	/*testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }*/
 }

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'Utils'
                description = 'Set of Java tools'
                url = 'https://github.com/JazZ51/Utils'
                developers {
                    developer {
                        id = 'mahdjoub'
                        name = 'Jason Mahdjoub'
                        email = 'jason.mahdjoub@distri-mind.fr'
                    }
                }
                organization {
                    name = 'DistriMind'
                    url = 'https://github.com/JazZ51'
                }
                issueManagement {
                    system = 'GitHub'
                    url = 'https://github.com/JazZ51/Utils/issues'
                }
                scm {
                    connection = 'https://github.com/JazZ51/Utils'
                }
                licenses {
                    license {
                        name = 'CeCILL-C FREE SOFTWARE LICENSE AGREEMENT'
                        url = 'http://cecill.info/licences/Licence_CeCILL-C_V1-en.txt'
                    }
                }

            }
        }
    }
    repositories {
        maven {
            credentials {
              username "${user}"
              password "${pwd}"
            }
            url "${artifactory_Url}/DistriMind-Public"
        }
    }
}
signing {
    useGpgCmd()
    required {!project.hasProperty('disableSigning')}
    if (required) {
        sign configurations.archives
        sign publishing.publications.mavenJava
    }

}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

