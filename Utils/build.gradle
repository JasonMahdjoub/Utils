/*
 * This build file was generated by the Gradle 'init' task.
 *
 * This generated file contains a sample Java Library project to get you started.
 * For more details take a look at the Java Libraries chapter in the Gradle
 * user guide available at https://docs.gradle.org/4.1/userguide/java_library_plugin.html
 */

// Apply the java-library plugin to add support for Java Library
apply plugin: 'java-library'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

compileJava.options.fork = true
compileJava.options.forkOptions.executable = "${jdk}/bin/javac"
compileJava.options.bootClasspath=fileTree(include: ['*.jar'], dir: "${jdk}/jre/lib").join(File.pathSeparator)
compileJava.options.extensionDirs = "${jdk}/jre/lib/ext/"

compileTestJava.options.fork = true
compileTestJava.options.forkOptions.executable = "${jdk}/bin/javac"
compileTestJava.options.bootClasspath=fileTree(include: ['*.jar'], dir: "${jdk}/jre/lib").join(File.pathSeparator)
compileTestJava.options.extensionDirs = "${jdk}/jre/lib/ext/"

eclipse {
    classpath {
       downloadSources=true
    }
}

// In this section you declare where to find the dependencies of your project
repositories {
    // Use jcenter for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    
        maven {
       		url "https://mahdjoub.net/artifactory/DistriMind-Public"
   		} 
    jcenter()
}

dependencies {
     testCompile 'org.testng:testng:6.13.1'
     compile(group:'com.berry', name: 'BCrypt', version: '1.3.1')
     compile(group:'org.bouncycastle', name: 'bc-core', version: '1.59b')
     compile(group:'org.bouncycastle', name: 'bc-prov', version: '1.59b')
     compile(group:'org.bouncycastle', name: 'bc-fips', version: '1.0.4')
     compile(group:'gnu', name: 'Gnu-Crypt', version: '1.2.1')
     compile(group:'commons-codec', name: 'commons-codec', version: '1.10')
    
}

allprojects {
	group='com.distrimind.util'
    repositories {
        jcenter() 
    }
    
}

sourceCompatibility = 1.7
targetCompatibility=1.7
version='3.7.1'


task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
    from "../LICENSE"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
    from "../LICENSE"
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

jar {
    manifest {
        attributes('Implementation-Title': project.name,
                   'Implementation-Version': project.version)
    }
    from "../LICENSE"

}

test {
     
     // enable TestNG support (default is JUnit)
   	useTestNG
   	{
   		suites 'src/test/java/com/distrimind/util/AllTestsNG.xml'
   	}
  
   // set heap size for the test JVM(s)
   minHeapSize = "256m"
   maxHeapSize = "1g"

 }

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            
        }
    }
    repositories {
        maven {
            credentials {
              username "${artifactory_user}"
              password "${artifactory_password}"
            }
            url "${artifactory_contextUrl}/DistriMind-Public"
        }
    }
}




